plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'org.jetbrains.dokka'
}

ext {
    bintrayRepo = 'oscleton-android-sdk'
    bintrayName = 'core'

    publishedGroupId = 'com.oscleton.sdk'
    artifact = 'core'
    libraryVersion = library_version

    libraryName = 'oscleton-android-sdk'
    libraryDescription = 'Ableton Live companion SDK for Android'

    siteUrl = 'https://sdk.oscleton.com/'
    gitUrl = 'https://github.com/ArthurVimond/oscleton-android-sdk.git'

    developerId = 'arthurvimond'
    developerName = 'Arthur Vimond'
    developerEmail = 'contact@oscleton.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 19
        versionName "$libraryVersion"
        buildConfigField 'String', 'VERSION_NAME', "\"$libraryVersion\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Support
    implementation 'androidx.appcompat:appcompat:1.1.0'

    // Lifecycle
    api 'androidx.lifecycle:lifecycle-extensions:2.1.0'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.1.12'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.5'

    // Koin
    implementation "io.insert-koin:koin-android:$koin_version"

    // OSC
    implementation 'com.illposed.osc:javaosc-core:0.4'

    // Add missing dependencies for JDK 9+
    // If you're using Kotlin
    kapt "com.sun.xml.bind:jaxb-core:2.3.0.1"
    kapt "javax.xml.bind:jaxb-api:2.3.1"
    kapt "com.sun.xml.bind:jaxb-impl:2.3.2"

    // If you're using Java
    annotationProcessor "com.sun.xml.bind:jaxb-core:2.3.0.1"
    annotationProcessor "javax.xml.bind:jaxb-api:2.3.1"

}

task dokka(overwrite: true, type: org.jetbrains.dokka.gradle.DokkaAndroidTask) {
    outputFormat = 'gfm'
    outputDirectory = "$projectDir/../doc/reference/android/core"
    // Do not create index pages for empty packages
    skipEmptyPackages = true
    // Do not output deprecated members. Applies globally, can be overridden by packageOptions
    skipDeprecated = false
    // No default documentation link to kotlin-stdlib
    noStdlibLink = false
}

if (project.rootProject.file('local.properties').exists()) {
    apply from: 'https://raw.githubusercontent.com/brightec/JCenter/master/installv1.gradle'
    apply from: 'https://raw.githubusercontent.com/brightec/JCenter/master/bintrayv1.gradle'
}