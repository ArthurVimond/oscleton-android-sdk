apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka'

ext {
    bintrayRepo = 'oscleton-android-sdk'
    bintrayName = 'core'

    publishedGroupId = 'fr.arthurvimond.oscletonsdk'
    artifact = 'core'
    libraryVersion = '0.3.0'

    libraryName = 'oscleton-android-sdk'
    libraryDescription = 'Ableton Live companion SDK for Android'

    siteUrl = 'https://github.com/ArthurVimond/oscleton-android-sdk'
    gitUrl = 'https://github.com/ArthurVimond/oscleton-android-sdk.git'

    developerId = 'arthurvimond'
    developerName = 'Arthur Vimond'
    developerEmail = 'arthurvimond@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 19
        versionName "$libraryVersion"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Support
    implementation "com.android.support:appcompat-v7:$support_version"

    // Lifecycle
    api "android.arch.lifecycle:extensions:$lifecycle_version"

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.1.12'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'

    // Koin
    implementation "org.koin:koin-android:$koin_version"

    // OSC
    implementation 'com.illposed.osc:javaosc-core:0.4'

}

task dokka(overwrite: true, type: org.jetbrains.dokka.gradle.DokkaAndroidTask) {
    outputFormat = 'gfm'
    outputDirectory = "$projectDir/../doc/docs/api-reference"
    // Do not create index pages for empty packages
    skipEmptyPackages = true
    // Do not output deprecated members. Applies globally, can be overridden by packageOptions
    skipDeprecated = false
    // No default documentation link to kotlin-stdlib
    noStdlibLink = false
}

if (project.rootProject.file('local.properties').exists()) {
    apply from: 'https://raw.githubusercontent.com/brightec/JCenter/master/installv1.gradle'
    apply from: 'https://raw.githubusercontent.com/brightec/JCenter/master/bintrayv1.gradle'
}
