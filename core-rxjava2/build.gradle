plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.dokka'
}

ext {
    bintrayRepo = 'oscleton-android-sdk'
    bintrayName = 'core-rxjava2'

    publishedGroupId = 'com.oscleton.sdk'
    artifact = 'core-rxjava2'
    libraryVersion = library_version

    libraryName = 'oscleton-android-sdk'
    libraryDescription = 'Ableton Live companion SDK for Android'

    siteUrl = 'https://sdk.oscleton.com/'
    gitUrl = 'https://github.com/ArthurVimond/oscleton-android-sdk.git'

    developerId = 'arthurvimond'
    developerName = 'Arthur Vimond'
    developerEmail = 'contact@oscleton.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.1.12'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'

    // Koin
    implementation "org.koin:koin-android:$koin_version"

    implementation project(':core')
}

task dokka(overwrite: true, type: org.jetbrains.dokka.gradle.DokkaAndroidTask) {
    outputFormat = 'gfm'
    outputDirectory = "$projectDir/../doc/reference/android/core-rxjava2"
    // Do not create index pages for empty packages
    skipEmptyPackages = true
    // Do not output deprecated members. Applies globally, can be overridden by packageOptions
    skipDeprecated = false
    // No default documentation link to kotlin-stdlib
    noStdlibLink = false
}

if (project.rootProject.file('local.properties').exists()) {
    apply from: 'https://raw.githubusercontent.com/brightec/JCenter/master/installv1.gradle'
    apply from: 'https://raw.githubusercontent.com/brightec/JCenter/master/bintrayv1.gradle'
}